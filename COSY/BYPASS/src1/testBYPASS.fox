INCLUDE 'SEQFULL_clean';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE MAPPARAMS 1 6; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 1281; VARIABLE SPNRARR 1000 3 3 1281; {element map arrays}
  VARIABLE MRKR 10;
  VARIABLE GAMMA 1;
  VARIABLE MAPSE 1;

  VARIABLE PNUM 1;
  VARIABLE PSI0_DEG 1;
  VARIABLE NTURN 1;

  MRKR := '';
  GROUTF 'img/dump/TR' 1;
  GAMMA := 1.14;
  NTURN := 30000;
  PSI0_DEG := 0;
  PNUM := 10;


  OV 3 3 0;
  { SET lattice parameters }
  SET_FOR_DEUTERONS GAMMA;
  LATTICE MAPPARAMS MAPARR SPNRARR;
     {print transfer map}
      OPENF 636 'TrMAP:' 'REPLACE';
      PM 636; CLOSEF 636;
      {pront spin transfer map}
      OPENF 636 'SpTrMAP:' 'REPLACE';
      PSM 636; CLOSEF 636;
      {print aberrations}
      OPENF 636 'ABERRATIONS:' 'REPLACE';
      PA 636; CLOSEF 636;
      {other stats about the map}
      MAPSE := SE(MAP);
      OPENF 636 'MAP-SYM-ERR:' 'REPLACE';
      WRITE 636 MAPSE; CLOSEF 636;
      WRITE 6 'MAP SYMPLECTIFICATION ERROR' MAPSE;


  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
